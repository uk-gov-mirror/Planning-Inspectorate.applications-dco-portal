resource "azurerm_virtual_network" "main" {
  name                = "${local.org}-vnet-${local.resource_suffix}"
  location            = module.primary_region.location
  resource_group_name = azurerm_resource_group.primary.name
  address_space       = [var.vnet_config.address_space]

  tags = var.tags
}

resource "azurerm_subnet" "apps" {

  #checkov:skip=CKV2_AZURE_31: "Ensure VNET subnet is configured with a Network Security Group (NSG)"

  name                              = "${local.org}-snet-${local.service_name}-apps-${var.environment}"
  resource_group_name               = azurerm_resource_group.primary.name
  virtual_network_name              = azurerm_virtual_network.main.name
  address_prefixes                  = [var.vnet_config.apps_subnet_address_space]
  private_endpoint_network_policies = "Enabled"

  # for app services
  delegation {
    name = "delegation"

    service_delegation {
      name = "Microsoft.Web/serverFarms"
      actions = [
        "Microsoft.Network/virtualNetworks/subnets/action"
      ]
    }
  }
}

resource "azurerm_subnet" "main" {

  #checkov:skip=CKV2_AZURE_31: "Ensure VNET subnet is configured with a Network Security Group (NSG)"

  name                              = "${local.org}-snet-${local.resource_suffix}"
  resource_group_name               = azurerm_resource_group.primary.name
  virtual_network_name              = azurerm_virtual_network.main.name
  address_prefixes                  = [var.vnet_config.main_subnet_address_space]
  private_endpoint_network_policies = "Enabled"
}

# peer to tooling VNET for DevOps agents
resource "azurerm_virtual_network_peering" "dcop_to_tooling" {
  name                      = "${local.org}-peer-${local.service_name}-to-tooling-${var.environment}"
  remote_virtual_network_id = data.azurerm_virtual_network.tooling.id
  resource_group_name       = azurerm_virtual_network.main.resource_group_name
  virtual_network_name      = azurerm_virtual_network.main.name
}

resource "azurerm_virtual_network_peering" "tooling_to_dcop" {
  name                      = "${local.org}-peer-tooling-to-${local.service_name}-${var.environment}"
  remote_virtual_network_id = azurerm_virtual_network.main.id
  resource_group_name       = var.tooling_config.network_rg
  virtual_network_name      = var.tooling_config.network_name

  provider = azurerm.tooling
}

# peer to back office for Service Bus connection
resource "azurerm_virtual_network_peering" "dcop_to_back_office" {
  # only deploy if configured to connect to back office
  count = var.back_office_infra_config == null ? 0 : 1

  name                      = "${local.org}-peer-${local.service_name}-to-back-office-${var.environment}"
  remote_virtual_network_id = data.azurerm_virtual_network.back_office_vnet[0].id
  resource_group_name       = azurerm_virtual_network.main.resource_group_name
  virtual_network_name      = azurerm_virtual_network.main.name
}

resource "azurerm_virtual_network_peering" "back_office_to_dcop" {
  # only deploy if configured to connect to back office
  count = var.back_office_infra_config == null ? 0 : 1

  name                      = "${local.org}-peer-back-office-to-${local.service_name}-${var.environment}"
  remote_virtual_network_id = azurerm_virtual_network.main.id
  resource_group_name       = var.back_office_infra_config.network.rg
  virtual_network_name      = var.back_office_infra_config.network.name
}

## DNS Zones for Azure Services
## Private DNS Zones exist in the tooling subscription and are shared here
resource "azurerm_private_dns_zone_virtual_network_link" "dns_database" {
  name                = "${local.org}-vnetlink-db-${local.resource_suffix}"
  private_dns_zone_id = data.azurerm_private_dns_zone.database.id
  virtual_network_id  = azurerm_virtual_network.main.id

  provider = azurerm.tooling
}

resource "azurerm_private_dns_zone_virtual_network_link" "app_service" {
  name                = "${local.org}-vnetlink-app-service-${local.resource_suffix}"
  private_dns_zone_id = data.azurerm_private_dns_zone.app_service.id
  virtual_network_id  = azurerm_virtual_network.main.id

  provider = azurerm.tooling
}

resource "azurerm_private_dns_zone_virtual_network_link" "redis_cache" {
  name                = "${local.org}-vnetlink-redis-cache-${local.resource_suffix}"
  private_dns_zone_id = data.azurerm_private_dns_zone.redis_cache.id
  virtual_network_id  = azurerm_virtual_network.main.id

  provider = azurerm.tooling
}

resource "azurerm_private_dns_zone_virtual_network_link" "keyvault" {
  name                = "${local.org}-vnetlink-keyvault-${local.resource_suffix}"
  private_dns_zone_id = data.azurerm_private_dns_zone.keyvault.id
  virtual_network_id  = azurerm_virtual_network.main.id

  provider = azurerm.tooling
}

resource "azurerm_private_dns_zone_virtual_network_link" "storage" {
  name                = "${local.org}-vnetlink-storage-${local.resource_suffix}"
  private_dns_zone_id = data.azurerm_private_dns_zone.storage.id
  virtual_network_id  = azurerm_virtual_network.main.id
  resolution_policy   = "NxDomainRedirect"

  provider = azurerm.tooling
}
