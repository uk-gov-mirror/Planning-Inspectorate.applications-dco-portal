module "app_portal" {
  #checkov:skip=CKV_TF_1: Use of commit hash are not required for our Terraform modules
  source = "github.com/Planning-Inspectorate/infrastructure-modules.git//modules/node-app-service?ref=1.57"

  resource_group_name = azurerm_resource_group.primary.name
  location            = module.primary_region.location

  # naming
  app_name        = "portal"
  resource_suffix = var.environment
  service_name    = "dco"
  tags            = local.tags

  # service plan & scaling
  app_service_plan_id                  = azurerm_service_plan.apps.id
  app_service_plan_resource_group_name = azurerm_resource_group.primary.name
  worker_count                         = var.apps_config.app_service_plan.worker_count

  # container
  container_registry_name = var.tooling_config.container_registry_name
  container_registry_rg   = var.tooling_config.container_registry_rg
  image_name              = "applications/dco-portal"

  # networking
  app_service_private_dns_zone_id = data.azurerm_private_dns_zone.app_service.id
  inbound_vnet_connectivity       = var.apps_config.private_endpoint_enabled
  integration_subnet_id           = azurerm_subnet.apps.id
  endpoint_subnet_id              = azurerm_subnet.main.id
  outbound_vnet_connectivity      = true
  # public access via Front Door
  front_door_restriction = true
  public_network_access  = true

  # monitoring
  action_group_ids                  = local.action_group_ids
  log_analytics_workspace_id        = azurerm_log_analytics_workspace.main.id
  monitoring_alerts_enabled         = var.alerts_enabled
  health_check_path                 = "/health"
  health_check_eviction_time_in_min = var.health_check_eviction_time_in_min

  #Easy Auth setting
  auth_config = {
    auth_enabled           = var.auth_config.auth_enabled
    require_authentication = var.auth_config.auth_enabled
    auth_client_id         = var.auth_config.auth_client_id
    #checkov:skip=CKV_SECRET_6: "Secret is securely stored in Key Vault"
    auth_provider_secret = "MICROSOFT_PROVIDER_AUTHENTICATION_SECRET"
    auth_tenant_endpoint = "https://login.microsoftonline.com/${data.azurerm_client_config.current.tenant_id}/v2.0"
    allowed_applications = var.auth_config.application_id
    allowed_audiences    = "https://${var.web_domains.portal}/.auth/login/aad/callback"
    excluded_paths       = []
  }

  app_settings = {
    IS_APPLICATION_ENABLED                     = var.apps_config.is_application_enabled
    APPLICATIONINSIGHTS_CONNECTION_STRING      = local.key_vault_refs["app-insights-connection-string"]
    ApplicationInsightsAgent_EXTENSION_VERSION = "~3"
    NODE_ENV                                   = var.apps_config.node_environment
    ENVIRONMENT                                = var.environment
    APP_HOSTNAME                               = var.web_domains.portal

    # logging
    LOG_LEVEL = var.apps_config.logging.level

    # database connection
    SQL_CONNECTION_STRING = local.key_vault_refs["sql-app-connection-string"]

    # Pdf-service-api URL
    PDF_SERVICE_URL = "https://${module.app_pdf.default_site_hostname}"

    # retries
    RETRY_MAX_ATTEMPTS = "3"
    # got default retry codes
    # https://github.com/sindresorhus/got/blob/main/documentation/7-retry.md
    RETRY_STATUS_CODES = "408,413,429,500,502,503,504,521,522,524"

    #Auth
    MICROSOFT_PROVIDER_AUTHENTICATION_SECRET = local.key_vault_refs["microsoft-provider-authentication-secret"]
    WEBSITE_AUTH_AAD_ALLOWED_TENANTS         = data.azurerm_client_config.current.tenant_id

    # sessions
    REDIS_CONNECTION_STRING = local.key_vault_refs["redis-connection-string"]
    SESSION_SECRET          = local.key_vault_refs["session-secret-web"]

    # gov notify
    GOV_NOTIFY_DISABLED                             = var.apps_config.gov_notify.disabled
    GOV_NOTIFY_API_KEY                              = local.key_vault_refs["dcop-gov-notify-api-key"]
    GOV_NOTIFY_OTP_TEMPLATE_ID                      = var.apps_config.gov_notify.templates.otp_template_id
    GOV_NOTIFY_WHITELIST_ADD_TEMPLATE_ID            = var.apps_config.gov_notify.templates.whitelist_add_template_id
    GOV_NOTIFY_WHITELIST_ACCESS_CHANGED_TEMPLATE_ID = var.apps_config.gov_notify.templates.whitelist_access_changed_templated_id
    GOV_NOTIFY_WHITELIST_REMOVE_TEMPLATE_ID         = var.apps_config.gov_notify.templates.whitelist_remove_templated_id
    GOV_NOTIFY_APPLICANT_SUBMISSION_TEMPLATE_ID     = var.apps_config.gov_notify.templates.applicant_submission_template_id
    GOV_NOTIFY_PINS_STAFF_SUBMISSION_TEMPLATE_ID    = var.apps_config.gov_notify.templates.pins_staff_submission_template_id
    GOV_NOTIFY_SUBMISSION_DATE_MISSED_TEMPLATE_ID   = var.apps_config.gov_notify.templates.submission_date_missed_template_id

    # blob store
    BLOB_STORE_DISABLED  = var.apps_config.blob_store.disabled
    BLOB_STORE_HOST      = data.azurerm_storage_account.back_office.primary_blob_endpoint
    BLOB_STORE_CONTAINER = azurerm_storage_container.documents.name

    # dummy case whitelist
    # todo: remove once cbos integration is complete
    CASE_WHITELIST = local.key_vault_refs["dcop-case-whitelist"]

    # service bus
    ServiceBusConnection__fullyQualifiedNamespace = "${var.back_office_config.service_bus_name}.servicebus.windows.net"
    SERVICE_BUS_PUBLISH_EVENT_DISABLED            = var.apps_config.service_bus_publish_event.disabled
    DCO_PORTAL_DATA_SUBMISSIONS_TOPIC             = data.azurerm_servicebus_topic.dco_portal_data_submissions.name
  }

  providers = {
    azurerm         = azurerm
    azurerm.tooling = azurerm.tooling
  }
}

## RBAC for secrets
resource "azurerm_role_assignment" "app_secrets_user" {
  scope                = azurerm_key_vault.main.id
  role_definition_name = "Key Vault Secrets User"
  principal_id         = module.app_portal.principal_id
}

## RBAC for secrets (staging slot)
resource "azurerm_role_assignment" "app_portal_staging_secrets_user" {
  scope                = azurerm_key_vault.main.id
  role_definition_name = "Key Vault Secrets User"
  principal_id         = module.app_portal.staging_principal_id
}

## sessions
resource "random_password" "web_session_secret" {
  length  = 32
  special = true
}

resource "azurerm_key_vault_secret" "web_session_secret" {
  #checkov:skip=CKV_AZURE_41: TODO: Secret rotation
  key_vault_id = azurerm_key_vault.main.id
  name         = "${local.service_name}-web-session-secret"
  value        = random_password.web_session_secret.result
  content_type = "session-secret"

  tags = local.tags
}
